// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoToThe;

    /** Initializes the library. */
    function void init() {
        var int i;
        let i = 1;
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        while (i < 16) {
            let twoToThe[i] = twoToThe[i-1] + twoToThe[i-1];
            let i = i + 1;
        }
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        } else {
            return x;
        }
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int j, sum, shiftedX;
        let j = 0;
        let sum = 0;
        let shiftedX = x;
        while (j < 16) {
            if (Math.bit(y, j)) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let j = j + 1;
        }
        return sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int x_abs, y_abs, j, q, r;
        var Array shiftedY;
        if (y = 0) {
            do Sys.error(3);
        }
        let x_abs = Math.abs(x);
        let y_abs = Math.abs(y);
        if (y_abs > x_abs) {
            return 0;
        }
        let j = 1;
        let shiftedY = Array.new(16);
        let shiftedY[0] = y_abs;
        while ((shiftedY[j-1] + shiftedY[j-1] > 0) & (shiftedY[j-1] + shiftedY[j-1] < x_abs)) {
            let shiftedY[j] = shiftedY[j-1] + shiftedY[j-1];
            let j = j + 1;
        }
        let q = 0;
        let r = x_abs;
        while (j > 0) {
            let j = j - 1;
            if (~(r < shiftedY[j])) {
                let q = q + q + 1;
                let r = r - shiftedY[j];
            } else {
                let q = q + q;
            }
        }
        if ((x > 0) = (y > 0)) {
            return q;
        } else {
            return -q;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, yp2j, yp2j2;
        if (x < 0) {
            do Sys.error(4);
        }
        let y = 0;
        let j = 7;
        while (~(j < 0)) {
            let yp2j = y + twoToThe[j];
            let yp2j2 = yp2j * yp2j;
            if (~(yp2j2 > x) & (yp2j2 > 0)) {
                let y = yp2j;
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        } else {
            return b;
        }
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        } else {
            return b;
        }
    }
    
    function boolean bit(int x, int j) {
        if ((x & twoToThe[j]) = 0) {
            return false;
        } else {
            return true;
        }
    }
}
